// Entry point
start: instructions*

// Collection constructs
?instructions: enum
            | ruleset
            | schema_entry

?values: INT -> integer
       | FLOAT -> float
       | string

schema_entry: "schema" "{" rule+ "}"

ruleset: "ruleset" /[A-Z]{1}[a-zA-Z0-9_]+/ "{" rule+ "}"

// Enum constructs
enum: "enum" /[A-Z]{1}[a-zA-Z0-9_]+/ "{" enum_item+ "}"
enum_item: /[A-Z0-9_]+/ "=" values

// Rule definitions
?rule: required_rule
     | optional_rule

required_rule: /[a-zA-Z0-9_]+/ type "required"
             | /[a-zA-Z0-9_]+/ type
optional_rule: /[a-zA-Z0-9_]+/ type "optional"

// Data types for rules
type: int_type
     | float_type
     | str_type
     | list_type
     | map_type
     | any_type
     | container_type
     | regex_type
     | bool_type

str_type: "str"
int_type: "int"
any_type: "any"
float_type: "float"
bool_type: "bool"
list_type: "list""(" type ")"
map_type: "map""(" type ")"
container_type: /[A-Z]{1}[a-zA-Z0-9_]+/
regex_type: "regex""(" string ")"

NEW_LINES: "\n"+

string: ESCAPED_STRING

%import common.SH_COMMENT
%import common.ESCAPED_STRING
%import common.INT
%import common.FLOAT
%import common.WS

%ignore WS
%ignore NEW_LINES
%ignore SH_COMMENT